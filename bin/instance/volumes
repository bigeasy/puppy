#!/usr/bin/env _coffee

ec2 = require "ec2"
fs = require "fs"
{OptionParser}  = require "coffee-script/lib/coffee-script/optparse"

parser = new OptionParser [
  [ "-n", "--name [NAME]", "fully qualified domain name" ]
  [ "-h", "--help", "display puppy help" ]
]

usage = (message) ->
  process.stderr.write "error: #{message}\n"
  process.stderr.write parser.help()
  process.stderr.write "\n"
  process.exit 1

try
  options         = parser.parse process.argv.slice(2)
catch e
  usage "Invalid arguments."

if not options.name
  usage "Host name is required."

match = ///
    ^
    \w+\.
    (balance|user|data)\.
    (north|south|east|west)\.
    (california|ireland|oregon|singapore|tokyo|virginia)\.
    runpup\.com
    $
  ///.exec(options.name)

if not match
  process.stderr.write "Invalid hostname.\n"
  usage()
[ type, zone, region ] = match.slice 1

switch type
  when "balance", "user"
    architecture = "i386"
  when "data"
    architecture = "x86_64"
  else
    usage "Unknown machine type."

zone = { north: "a", south: "b", east: "c", west: "d" }[zone]
if not zone
  usage "Unknown zone."

endpoint = { virginia: "us-east-1", oregon: "us-west-2", california: "us-west-1" }[region]
if not endpoint
  usage "Unknown region."

do (_ = (error) -> throw error if error) ->
  configuration = JSON.parse fs.readFile "#{process.env["HOME"]}/.aws", "utf8", _
  configuration.endpoint = endpoint
  ec2 = ec2 configuration

  response = ec2 "DescribeInstances", {}, _
  instance = do ->
    for reservation in response.reservationSet
      for instance in reservation.instancesSet
        if instance.instanceState.name is "running"
          for tag in instance.tagSet
            if tag.key is "Name" and tag.value is options.name
              return instance
  response = ec2 "DescribeVolumes", {}, _
  volumes = []
  for volume in response.volumeSet
    tags = {}
    for tag in volume.tagSet or []
      tags[tag.key] =  tag.value or true
    volume.tags = tags
    if tags.Puppified and tags.Host is options.name
      if volume.status isnt "available"
        attachment = volume.attachmentSet[0]
        if attachment.instanceId is request.instance.instanceId
          continue
        usage "The volume #{volume.tags.Name} is not available."
      volumes.push volume
  for volume in volumes
    ec2 "AttachVolume", {
      InstanceId: instance.instanceId
      VolumeId: volume.volumeId
      Device: volume.tags.Device
    }
    loop
      repsonse = ec2 "DescribeVolumes", { "VolumeId.1": volume.volumeId }, _
      break if response.volumeSet[0].attachmentSet[0].status is "attached"
# vim: ft=coffee ts=2 sw=2:
