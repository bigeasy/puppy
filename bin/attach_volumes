#!/usr/bin/env coffee

ec2 = require "ec2"
{OptionParser}  = require "coffee-script/lib/optparse"

parser = new OptionParser [
  [ "-n", "--name [NAME]", "fully qualified domain name" ]
  [ "-h", "--help", "display puppy help" ]
]

usage = (message) ->
  process.stderr.write "error: #{message}\n"
  process.stderr.write parser.help()
  process.stderr.write "\n"
  process.exit 1

try
  options         = parser.parse process.argv.slice(2)
catch e
  usage "Invalid arguments."

if not options.name
  usage "Host name is required."

match = ///
    ^
    \w+\.
    (balance|user|data)\.
    (north|south|east|west)\.
    (california|ireland|singapore|tokyo|virginia)\.
    runpup\.com
    $
  ///.exec(options.name)

if not match
  process.stderr.write "Invalid hostname.\n"
  usage()
[ type, zone, region ] = match.slice 1

switch type
  when "balance", "user"
    architecture = "i386"
  when "data"
    architecture = "x86_64"
  else
    usage "Unknown machine type."

zone = { north: "a", south: "b", east: "c", west: "d" }[zone]
if not zone
  usage "Unknown zone."

region = { virginia: "us-east-1" }[region]
if not region
  usage "Unknown region."

client = ec2.createClient({
  key: process.env["AWS_ACCESS_KEY_ID"]
  secret: process.env["AWS_SECRET_ACCESS_KEY"]
})

request = {}
client.call "DescribeInstances", {}, (response) ->
  findInstance = ->
    for reservation in response.reservationSet
      for instance in reservation.instancesSet
        if instance.instanceState.name is "running"
          for tag in instance.tagSet
            if tag.key is "Name" and tag.value is options.name
              return instance
    false
  request.instance = findInstance()
client.call "DescribeVolumes", {}, (response) ->
  request.volumes = []
  for volume in response.volumeSet
    tags = {}
    for tag in volume.tagSet or []
      tags[tag.key] =  tag.value or true
    if tags.Puppified and tags.Host is options.name
      request.volumes.push volume
    volume.tags = tags
  volumes = []
  for volume in request.volumes
    if volume.status isnt "available"
      attachment = volume.attachmentSet[0]
      if attachment.instanceId is request.instance.instanceId
        continue
      usage "The volume #{volume.tags.Name} is not available."
    volumes.push volume
  request.volumes = volumes
  for volume in request.volumes
    client.call "AttachVolume", {
      InstanceId: request.instance.instanceId
      VolumeId: volume.volumeId
      Device: volume.tags.Device
    }
    client.poll "DescribeVolumes", {
      "VolumeId.1": volume.volumeId
    }, (response) ->
      process.stdout.write JSON.stringify response, null, 2
      process.stdout.write "\n"
      response.volumeSet[0].attachmentSet[0].status is "attached"

client.execute()
